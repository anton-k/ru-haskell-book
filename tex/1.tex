\setcounter{chapter}{0}
\chapter{Основы}

Есть мнение, что Haskell очень большой язык. Это и правда так.
В Haskell много разных конструкций, синтаксического сахара, которые
делают код более наглядным. Также в Haskell много библиотек на
разные случаи жизни. Однако, обману ли я 
ваши ожидания, сказав, что всё это имеет достаточно компактную 
основу? Это и правда так, вам осталось лишь убедиться в 
наглядности и простоте Haskell. В этой главе мы пробежимся по 
нему, охватив одним взглядом целиком весь язык. Несколько 
наглядных конструкций, немного моих пояснений, и вы поймёте, что 
к чему. Если что-то сразу не станет ясно, или где-то я опущу 
какие-то пояснения, будьте уверены -- в следующих главах мы 
обязательно обратимся к этим моментам и обсудим их подробнее. 

\section{Общая картина}


Программы на Haskell бывают двух видов: это 
\emph{приложения} (executable) и \emph{библиотеки} (library).
Приложения представляют собой исполняемые файлы, которые решают
некоторую задачу, к примеру -- это может быть компилятор языка, 
сортировщик данных в директориях, календарь, или цитатник на каждый день,
любая полезная утилита.
Библиотеки тоже решают задачи, но решают их внутри самого языка.
Они содержат отдельные значения, функции, которые можно подключать
к другой программе Haskell, и которыми можно пользоваться.


Программа состоит из \index{модуль} \emph{модулей} (module). И здесь работает 
правило: один модуль -- один файл. Имя модуля совпадает с именем файла.
Имя модуля начинается с большой буквы, тогда как файлы имеют 
расширение \In{.hs}.
Например \In{FirstModule.hs}. Посмотрим на типичный модуль в Haskell:

\begin{code}
--------------------------------------
-- шапка

module Имя(определение1, определение2,..., определениеN) where

import Модуль1(...)
import Модуль2(...)
...

---------------------------------------
-- определения

определение1
определение2
...
\end{code}



Каждый модуль содержит набор 
определений. Относительно модуля определения делятся на 
\emph{экспортируемые} и \emph{внутренние}. Экспортируемые определения 
могут быть использованы за пределами модуля, а внутренние -- только
внутри модуля, и обычно они служат для выражения экспортируемых определений.

Модуль состоит из двух частей -- шапки и определений.

\EmphDesc{Шапка}{%
    В шапке после слова \KeyWord{module} \In{module} объявляется имя модуля, 
    за которым в скобках следует список экспортируемых 
    определений; после скобок стоит слово \In{where}.
    Затем идут импортируемые модули. С помощью импорта
    модулей вы имеете возможность в данном модуле пользоваться определениями
    из другого модуля.  }

\EmptyDesc{%    
    Как после имени модуля, так и в директиве 
    \KeyWord{import} \In{import} скобки с определениями можно не писать,так 
    как в этом     случае считается, что экспортируются/импортируются все 
    определения.
}

\EmphDesc{Определения}{%
    Эта часть содержит все определения модуля, при этом порядок 
    следования определений не имеет значения. То есть, не
    обязательно пользоваться в данной функции лишь теми значениями,
    что были определены выше.
}

\smallskip

Модули взаимодействуют друг с другом с помощью экспортируемых определений.
Один модуль может сказать, что он хочет воспользоваться экспортируемыми
определениями другого модуля, для этого он пишет 
\In{import Модуль(определения)}. Модуль -- это айсберг, 
на вершине которого -- те функции, ради которых он 
создавался (экспортируемые), а под водой -- все служебные 
детали реализации (внутренние).

Итак, программа состоит из модулей, модули состоят из определений.
Но что такое определения?

В Haskell определения могут описывать четыре вида сущностей:

\begin{itemize}

\item Типы.

\item Значения.

\item Классы типов.

\item Экземпляры классов типов.

\end{itemize}

Теперь давайте рассмотрим их подробнее.


\subsection{Типы}

\index{Типы}
Типы представляют собой каркас программы. Они кратко
описывают все возможные значения. Это очень удобно.
Опытный программист на Haskell может понять смысл
функции по её названию и типу. Это не очень сложно.
Например, мы видим:

\begin{code}
not :: Bool -> Bool
\end{code}

Выражение \In{v :: T} означает, что значение
\In{v} имеет тип \In{T}. Стрелка \mbox{\In{a -> b}}
означает функцию, то есть из \In{a} мы можем получить \In{b}.
Итак, перед нами функция из \In{Bool} в \In{Bool},
под названием \In{not}. Мы можем предположить, что
это логическая операция \Quote{не}. 
Или, перед нами такое определение типа:

\begin{code}
reverse :: [a] -> [a]
\end{code}

Мы видим функцию с именем \In{reverse}, которая принимает
список \mbox{\In{[a]}} и возвращает список \In{[a]}, и мы
можем догадаться, что эта функция переворачивает список, то есть
мы получаем список, у которого элементы идут в обратном порядке.
Маленькая буква \In{a} в \In{[a]} является параметром типа,
на место параметра может быть поставлен любой тип.
Она говорит о том, что список содержит элементы типа \In{a}.
Например, такая функция соглашается переворачивать только
списки логических значений:

\begin{code}
reverseBool :: [Bool] -> [Bool]
\end{code}

Программа представляет собой описание
некоторого явления или процесса. Типы определяют 
основные слова или термины и способы их комбинирования.
А значения представляют собой комбинации базовых 
слов. Но значения комбинируются не произвольным
образом, а на основе определённых правил, которые 
задаются типами. 

Например, такое выражение определяет тип, в котором
два базовых термина \Types{Bool, логические значения} \In{True} или \In{False}

\begin{code}
data Bool = True | False
\end{code}

Слово \KeyWord{data} \In{data} ключевое, с него 
начинается любое определение нового
типа. Символ \In{|} означает или. Наш новый тип \In{Bool}
является либо словом \In{True}, либо словом \In{False}.
В этом типе есть только понятия, но нет способов комбинирования,
посмотрим на тип, в котором есть и то, и другое:

\begin{code}
data [a] = [] | a : [a]
\end{code}

\Types{[a], списки}
Это определение списка. Как мы уже поняли, \In{a} -- это параметр.
Список \In{[a]} может быть либо пустым списком \In{[]}, либо
комбинацией \In{a : [a]}. В этой комбинации знак \In{:} объединяет
элемент типа \In{a} и ещё один список \In{[a]}. Это рекурсивное
определение, они встречаются в Haskell очень часто.
Если это пока кажется непонятным, не пугайтесь, в следующих
главах будет представлено много примеров с пояснениями.

Приведём ещё несколько примеров определений; ниже
типы определяют базовые понятия для мира календаря: 
то что стоит за \verb!--! является
комментарием и игнорируется при выполнении программы: 

\begin{code}
-- Дата
data Date = Date Year Month Day

-- Год
data Year  = Year Int       -- Int это целые числа

-- Месяц
data Month  = January    | February   | March    | April          
            | May        | June       | July     | August   
            | September  | October    | November | December 

data Day = Day Int

-- Неделя
data Week  = Monday     | Tuesday   | Wednesday 
           | Thursday   | Friday    | Saturday     
           | Sunday     

-- Время
data Time = Time Hour Minute Second

data Hour   = Hour   Int    -- Час
data Minute = Minute Int    -- Минута
data Second = Second Int    -- Секунда
\end{code}

Одной из основных целей разработчиков Haskell была 
ясность. Они стремились создать язык, предложения которого
будут простыми и понятными, близкий к языку спецификаций.

С символом \In{|} мы уже познакомились, он указывает на альтернативы,
объединение пишется через пробел. Так, фраза

\begin{code}
data Time = Time Hour Minute Second
\end{code}

\noindent означает, что тип \In{Time} -- это значение с меткой \In{Time},
которое состоит из значений типов \Quote{час}, \Quote{время} и 
\Quote{секунда}, и больше ничего. Метку принято называть 
\emph{конструктором}.

Фраза

\begin{code}
data Year = Year Int
\end{code}

\noindent означает, что тип \In{Year} -- это значение с 
конструктором \In{Year},
которое состоит из одного значения типа \In{Int}.
Конструктор обычно идёт первым, а за ним через пробел следуют 
другие типы. Конструктор может быть и самостоятельным значением,
как в случае \In{True} или \In{January}.

Типы делят выполнение программы на две стадии: \emph{компиляцию}
(compile-time) и \emph{вычисление} (run-time). На этапе 
компиляции происходит проверка типов. Программа, которая
не прошла проверку типов, считается бессмысленной и не
вычисляется. Приложение, которое выполняет компиляцию,
называют \index{компилятор}
\emph{компилятором} (compiler), а то приложение, которое 
проводит вычисление, называют \emph{вычислителем} (run-time system).

Типами мы определяем основные понятия в том явлении,
которое мы хотим описать, а также осмысленные способы
их комбинирования. Мы говорим, как из простейших терминов
получаются составные. 
Если мы попытаемся построить 
бессмысленное предложение, компилятор языка автоматически 
найдёт такое предложение и сообщит нам об этом. 
Этот процесс заключается в проверке
типов, к примеру если у нас есть функция сложения чисел, и 
мы попытаемся передать в неё строку или список, компилятор
заметит это и скажет нам об этом \emph{перед} тем как программа
начнёт выполнятся. И важно то, что это произойдёт очень быстро.
Если мы случайно ошиблись в выражении, которое будет вычислено
через час, нам не нужно ждать пока вычислитель дойдёт до ошибки, мы
узнаем об этом, не успев моргнуть, после запуска программы.

Итак, если мы попробуем составить время из месяцев и логических
значений:

\begin{code}
Time January True 23
\end{code}

\noindent компилятор предупредит нас об ошибке. Наверное, вы 
думаете, что приведенный пример надуман, ведь кому 
захочется составлять время из логических значений? Но когда 
вы пишете программу, часто процесс работы складывается так:
вы думаете над одним, пишете другое, а также планируете вернуться
к третьему. И знание того, что есть надежный компилятор, который
не пропустит глупых ошибок, освобождает руки, вы можете не 
заботиться о таких пустяках, как правильное построение предложения. 

Отметим, что такой подход с разделением вычисления на
две стадии и проверкой типов называется 
\Types{статическая типизация}
\emph{статической типизацией}.
Есть и другие языки, в них типы лишь подразумеваются 
и программа сразу начинает вычисляться, если есть какие-то
несоответствия, об ошибке программисту сообщит вычислитель,
причём только тогда, когда вычисление дойдёт до ошибки.
\Types{динамическая типизация}
Такой подход называют \emph{динамической типизацией}.

Типы требуют серьёзных размышлений на начальном этапе, этапе определения
базовых терминов и способов их комбинирования. Не упускаем ли
мы что-то важное из виду, или, может быть, типы имеют слишком 
общий характер и допускают ненужные нам предложения? Приходится 
задумываться. Но если типы подобраны удачно, они сами
начинают подсказывать, как строить программу.

\subsection{Значения}

Итак, мы определили типами базовые понятия и способы комбинирования.
Обычно это небольшой набор слов. Например в логических выражениях
всего лишь два слова. Можем ли мы на что либо рассчитывать с 
таким словарным запасом? Оказывается, что да. Здесь на помощь приходят синонимы. Сейчас у нас в активе лишь два слова:

\begin{code}
data Bool = True | False
\end{code}

И мы можем определить два синонима:

\begin{code}
true :: Bool
true = True

false :: Bool
false = False
\end{code}

В Haskell синонимы пишутся с маленькой буквы. Синоним 
определяется через знак \In{=}. Обратите внимание на то, что
это не процесс вычисления значения. Мы всего лишь объявляем 
новое имя для комбинации слов. 

Теперь мы имеем целых четыре слова! Тем не менее, ушли мы не далеко, и два новых
слова, в сущности, не делают язык выразительнее.
Такие синонимы называют \emph{константами}. Это значит, что одним словом мы будем обозначать некоторую комбинацию других слов.
В данном случае комбинации очень простые.

Но наши синонимы могут определять одни слова через другие. 
Синонимы могут принимать параметры. Параметры пишутся через
пробел между новым именем и знаком равно:

\begin{code}
not :: Bool -> Bool
not True  = False
not False = True
\end{code}

Мы определили новое имя \In{not} с типом \In{Bool -> Bool}. 
Оно определяется двумя \emph{уравнениями} (clause). Слева
от знака равно левая часть уравнения, а справа -- правая.
В первом уравнении мы говорим, что сочетание \In{(not True)}
означает \In{False}, а сочетание \In{(not False)} означает
\In{True}. Опять же, мы ничего не вычисляем, мы даём новые имена
нашим константам \In{True} и \In{False}. Только в этом случае
имена составные. 

Если вычислителю нужно узнать, что кроется за составным именем
\mbox{\In{not False}}
он \emph{последовательно} проанализирует уравнения сверху вниз, до тех
пор, пока левая часть уравнения не совпадёт со значением \In{not False}.
Сначала мы сверим с первым:

\begin{code}
not True   == not False     -- нет, пошли дальше
not False  == not False     -- эврика, вернём правую часть
=> True
\end{code}



Определим ещё два составных имени 

\begin{code}
and :: Bool -> Bool -> Bool
and False  _  = False
and True   x  = x

or  :: Bool -> Bool -> Bool
or True   _ = True
or False  x = x
\end{code}

Эти синонимы определяют логические операции \Quote{и}
и \Quote{или}. Здесь несколько новых конструкций, 
но вы не пугайтесь, они не так трудны для понимания. Начнём с \In{_}:

\begin{code}
and False  _  = False
\end{code}

Здесь cимвол \In{_} означает, что в этом уравнении, 
если первый параметр равен \In{False}, то второй нам уже 
не важен, мы знаем ответ. Так, если в логическом \Quote{и}
один из аргументов равен \In{False}, то всё выражение
равно \In{False}. Так же и в случае с \In{or}.

Теперь другая новая конструкция:

\begin{code}
and True   x  = x
\end{code}

В этом случае параметр \In{x} служит для того, чтобы 
перетащить значение из аргумента в результат. 
Конкретное значение нам также не важно, но в этом
случае мы полагаем, что слева и справа от \In{=},
\In{x} имеет одно и то же значение.

Итак у нас уже целых семь имён: \In{True}, \In{False},
\In{true}, \In{false}, \In{not}, \In{and}, \In{or}. Или не семь? 
На самом деле, их уже бесконечное множество. Поскольку
три из них составные, мы можем создавать самые разнообразные 
комбинации:

\begin{code}
not (and true False)
or (and true true) (or False False)
not (not true)
not (or (or True True) (or False (not True)))
...
\end{code}

Обратите внимание на использование скобок, они группируют 
значения. Так, если бы мы написали \In{not not true} вместо
\In{not (not true)}, мы бы получили ошибку компиляции, потому
что \In{not} ожидает один параметр, а в выражении \In{not not true}
их два. Параметры дописываются к имени через пробел.

Посмотрим, как происходят вычисления. В сущности, 
процесса вычислений нет, есть процесс замены синонимов
на основные понятия согласно уравнениям. Базовые понятия мы 
определили в типах. 
Так давайте \Quote{вычислим} выражение \In{not (and true False)}:

\begin{code}
-- выражение            --  уравнение

not (and true False)    --  true        = True
not (and True False)    --  and True  x = x     => and True False = False
not False               --  not False   = True
True
\end{code}

Слева в столбик написаны шаги \Quote{вычисления}, а справа уравнения, по которым синонимы заменяются на комбинации
слов. Процесс замены синонима (левой части уравнения) на
комбинацию слов (правую часть уравнения) называется 
\index{редукция} \emph{редукцией}
(reduction). 

Сначала мы заменили синоним \In{true} на правую часть
его уравнения, т.е.~на конструктор \In{True}. Затем мы заменили
выражение \In{(and True False)} на правую часть из уравнения
для синонима \In{and}. Обратите внимание на то, что переменная
\In{x} была заменена на значение \In{False}.
Последним шагом была замена синонима
\In{not}. В конце концов мы пришли к базовому понятию, а именно -- к 
одному из двух конструкторов. В данном случае \In{True}.

Интересно, что новые синонимы могут быть использованы 
в правых частях уравнений. Так мы можем определить операцию
\Quote{исключающее или}:

\begin{code}
xor :: Bool -> Bool -> Bool
xor a b = or (and (not a) b) (and a (not b))
\end{code}

Этим выражением мы говорим, что \In{xor a b} это
или отрицание \In{a} и \In{b}, или \In{a} и отрицание \In{b}.
Это и есть определение \Quote{исключающего или}.

Может показаться, что с типом \In{Bool} мы зациклены
на двух конструкторах, и единственное, что нам остаётся -- 
это давать всё новые и новые имена словам \In{True}
и \In{False}. Но на самом деле это не так. 
С помощью типов-параметров мы можем выйти за эти рамки.
Определим функцию ветвления \In{ifThenElse}:

\begin{code}
ifThenElse :: Bool -> a -> a -> a
ifThenElse True   t  _ = t
ifThenElse False  _  e = e
\end{code}

Эта функция первым аргументом принимает значение
типа \In{Bool}, а вторым и третьим -- альтернативы 
некоторого типа \In{a}. Если первый аргумент --
\In{True}, возвращается второй аргумент, а если -- \In{False},
то третий. 

Интересно, что в Haskell ничего не происходит,
мир Haskell-значений стоит на месте. Мы просто даём 
имена разным комбинациям слов. Определяем новые термины.
Потом на этих терминах определяем  новые термины,
и так далее. Кажется, если ничего не меняется, то зачем 
язык? И что мы собираемся программировать без вычислений? 

Разгадка кроется в функциях \In{not}, \In{and} и \In{or}.
До того как мы их определили, у нас было четыре имени, но
после их определения имён стало бесконечное множество.
Три синонима пополнили наш язык бесконечным набором
комбинаций. В этом суть. Мы определяем базовые элементы
и способы составления новых, потом мы просим 
\Quote{вычислить} комбинацию из них. Мы не определяли
явно, 
чему равна комбинация \In{not (and true False)}, это сделал за 
нас вычислитель Haskell
\footnote{Было бы точнее называть вычислитель редуктором,
поскольку мы проводим редукции, т.е.~замену эквивалентных
значений, но закрепилось это название. К тому же, редуктор также обозначает прибор.}.

Вычислить стоит в кавычках, потому что на самом деле
вычислений нет, есть замена синонимов на комбинации простейших
элементов.


Ещё один пример, положим у нас есть тип:

\begin{code}
data Status = Work | Rest
\end{code}

Он определяет, что делать в данный день: работать (\In{Work})
или отдыхать (\In{Rest}). У разных рабочих разный график.
Например, есть функции:

\begin{code}
jonny :: Week -> Status
jonny x = ...

colin :: Week -> Status
colin x = ...
\end{code}

Конкретное определение сейчас не важно, важно, что они определяют
зависимость статуса (\In{Status}) от дня недели (\In{Week}) для 
работников Джонни (\In{jonny}) и Колина (\In{colin}). 

Также у нас есть полезная функция:

\begin{code}
calendar :: Date -> Week
calendar x = ...
\end{code}

Она определяет по дате день недели. И теперь, зная лишь
эти функции, мы можем спросить у вычислителя будет ли
у Джонни выходной 8 августа 3043 года:

\begin{code}
jonny (calendar (Date (Year 3043) August (Day 8)))
=> jonny Saturday
=> Rest
\end{code}

Интересно, у нас опять всего лишь два значения, 
но, дав такое большое имя одному из значений,
мы смогли получить полезную нам информацию,
ничего не вычисляя.


\subsection{Классы типов}

Если типы и значения -- привычные понятия, которые можно найти
в том или ином виде в любом языке программирования, то термин 
класс типов встречается не часто. У него нет аналогов и в 
обычном языке, поэтому я сначала постараюсь объяснить 
его смысл на примере. 

В типизированном языке у каждой 
функции есть тип, но бывают функции, которые могут быть 
определены на аргументах разных типов; по сути, они описывают
схожие понятия, но определены для значений разных типов.
Например, функция сравнения на равенство, говорящая о том, что два значения 
одного типа \In{a} равны, имеет тип \In{a -> a -> Bool}, 
или функция печати выражения имеет тип \In{a -> String},
но что такое \In{a} в этих типах? Тип \In{a} является
любым типом, для которого сравнение на равенство или 
печать (преобразование в строку) имеют смысл. Это
понятие как раз и кодируется в классах типов. 
\index{Классы типов} \emph{Классы типов} (type class)
позволяют определять функции с одинаковым
именем для разных типов. 

У классов типов есть имена. Также как и имена классов, 
они начинаются с большой буквы. Например, класс сравнений 
на равенство называется \Classes{Eq, сравнение на равенство} 
\In{Eq} (от англ. \emph{equals} -- равняется),
а класс печати выражений имеет имя \In{Show} (от англ. \emph{show} 
-- показывать). Посмотрим на их определения:

Класс  \In{Eq}:

\begin{code}
class Eq a where
    (==) :: a -> a -> Bool
    (/=) :: a -> a -> Bool
\end{code}

Класс  \Classes{Show, печать значений} \In{Show}:

\begin{code}
class Show a where
    show :: a -> String
\end{code}

За ключевым словом \KeyWord{class} \In{class} следует имя класса, тип-параметр
и ещё одно ключевое слово \In{where}. Далее с отступами пишутся
имена определённых в классе значений. Значения класса называются
\Classes{методы} \emph{методами}.

Мы определяем лишь типы методов, конкретная реализация будет
зависеть от типа \In{a}. Методы определяются в экземплярах
классов типов, мы скоро к ним перейдём.

Программистская аналогия
класса типов это интерфейс. В интерфейсе определён набор
значений (как констант, так и функций), которые могут быть применены 
ко всем типам, которые поддерживают данный интерфейс.
К примеру, в интерфейсе \Quote{сравнение на равенство} для некоторого
типа \In{a} определены две функции: равно \In{(==)} и 
не равно \In{(/=)} с одинаковым
типом \In{a -> a -> Bool}, или в интерфейсе \Quote{печати} для
любого типа \In{a} определена одна функция \In{show} типа \In{a -> String}.

Математическая аналогия 
класса типов это алгебраическая система. Алгебра 
изучает свойства объекта в терминах операций, определённых на нём,
и взаимных ограничениях этих операций. Алгебраическая система представляет
собой набор операций и свойств этих операций. Этот подход позволяет
абстрагироваться от конкретного представления объектов. Например группа -- 
это все объекты данного типа \In{a},  для которых определены
значения: константа -- единица типа \In{a}, бинарная операция
типа \In{a -> a -> a} и операция взятия обратного элемента,
типа \In{a -> a}. При этом на операции накладываются 
ограничения, называемые свойствами операций. Например, ассоциативность
бинарной операции, или тот факт, что единица с любым другим
элементом, применённые к бинарной операции, дают на выходе
исходный элемент.

Давайте определим класс для группы:

\begin{code}
class Group a where
    e   :: a
    (+) :: a -> a -> a
    inv :: a -> a
\end{code}

Класс с именем \In{Group} имеет для некоторого типа \In{a} 
три метода: константу \In{e :: a}, операцию \In{(+) :: a -> a -> a}
и операцию взятия обратного элемента \In{inv :: a -> a}.

Как и в алгебре, в Haskell классы типов позволяют описывать 
сущности в терминах, определённых на них операций или значений.
В примерах мы указываем лишь наличие операций и их типы, так же
и в классах типов. Класс типов содержит набор имён его значений 
с информацией о типах значений.

Определив класс \In{Group}, мы можем начать строить 
различные выражения, которые будут потом интерпретироваться
специфическим для типа образом:

\begin{code}
twice :: Group a => a -> a
twice a = a + a

isE :: (Group a, Eq a) => a -> Bool
isE x = (x == e)
\end{code}

Обратите внимание на запись \In{Group a =>} и 
\In{(Group a, Eq a) =>}. Это называется контекстом функции. 
В контексте мы говорим, что данный тип должен быть из 
типа \In{Group} или из типов \In{Group} и \In{Eq}. Это
значит, что для этого типа мы можем пользоваться методами
из этих классов.

В первой функции \In{twice} мы воспользовались методом
\In{(+)} из класса \In{Group}, поэтому функция имеет
контекст \In{Group a =>}. А во второй функции \In{isE} 
мы воспользовались методом \In{e} из класса \In{Group} и
методом \In{(==)} из класса \In{Eq}, поэтому функция
имеет контекст \In{(Group a, Eq a) =>}.

\subsection{Контекст классов типов. Суперклассы}

Класс типов также может содержать контекст. Он указывается
между словом \In{class} и именем класса. Например


\begin{code}
class IsPerson a

class IsPerson a => HasName a where
    name :: a -> String
\end{code}

Это определение говорит о том, что мы можем сделать 
экземпляр класса \In{HasName} 
только для тех типов, которые содержатся в \In{IsPerson}.
Мы говорим, что класс \In{HasName} содержится в \In{IsPerson}.
В этом случае класс из контекста \In{IsPerson} 
называют \Classes{суперкласс}
\emph{суперклассом} для данного класса \In{HasName}.

Это сказывается на контексте функции. Теперь, если мы пишем


\begin{code}
fun :: HasName a => a -> a
\end{code}


Это означает, что мы можем пользоваться для значений типа
\In{a} как методами из класса \In{HasName}, так и методами 
из класса \In{IsPerson}. Поскольку если тип принадлежит 
классу \In{HasName}, то он также принадлежит и \In{IsPerson}. 

Запись \In{(IsPerson a => HasName a)} немного обманывает, было бы точнее 
писать \In{IsPerson a <= HasName a}, если тип \In{a} в классе \In{HasName}, то 
он точно в классе \In{IsPerson}, но в Haskell  закрепилась другая запись.

\subsection{Экземпляры классов типов}

В \index{экземпляры классов типов}
\emph{экземплярах} (instance) классов типов мы даём конкретное
наполнение для методов класса типов. Определение экземпляра
пишется так же, как и определение класса типа, но вместо \In{class}
мы пишем \KeyWord{instance} \In{instance}, вместо некоторого типа наш конкретный
тип, а вместо типов методов -- уравнения для них. 

Определим экземпляры для \In{Bool}

Класс \In{Eq}:

\begin{code}
instance Eq Bool where
    (==) True  True  = True
    (==) False False = True
    (==) _     _     = False

    (/=) a b         = not (a == b)
\end{code}


Класс \In{Show}:

\begin{code}
instance Show Bool where
    show True  = "True"
    show False = "False"
\end{code}

Класс \In{Group}:

\begin{code}
instance Group Bool where
    e       = True
    (+) a b = and a b
    inv a   = not a
\end{code}



Отметим важность наличия свойств (ограничений) у значений,
определённых в классе типов. Так, например, в классе типов 
\Quote{сравнение на равенство} для любых двух значений данного типа
одна из операций должна вернуть \Quote{истину}, а другая \Quote{ложь},
т.е.~два элемента данного типа либо равны, либо не равны.
Недостаточно определить равенство для конкретного типа, необходимо
убедиться в том, что для всех элементов данного типа свойства 
понятия равенства не нарушаются.

На самом деле приведённое выше определение экземпляра для \In{Group} 
не верно, хотя по типам оно подходит. Оно не верно как раз 
из-за нарушения свойств. Для группы необходимо, чтобы для любого \In{a}
выполнялось:

\begin{code}
 inv a + a == e
\end{code}

У нас лишь два значения, и это свойство не выполняется ни для одного
из них. Проверим:

\begin{code}
    inv True   + True
 => (not True) + True
 => False      + True
 => and False    True
 => False

    inv False   + False
 => (not False) + False
 => True        + False
 => and True      False
 => False
\end{code}

Проверять свойства очень важно, потому что другие люди,
читая ваш код и используя ваши функции, будут на них рассчитывать.

\section{Ядро Haskell}

Фуууухх. Мы закончили наш пробег. Теперь можно остановиться,
отдышаться и подвести итоги.
Давайте вспомним синтаксические конструкции, которые нам встретились.

\subsubsection{Модули}

\begin{code}
module New(edef1, edef2, ..., edefN) where

import Old1(idef11, idef12, ..., idef1N)
import Old2(idef21, idef22, ..., idef2M)
...
import OldK(idefK1, idefK2, ..., idefKP)

-- определения :
...
\end{code}

Ключевые слова: \In{module}, \In{where}, \In{import}. Мы определили
модуль с именем \In{New}, который экспортирует определения
\In{edef1}, \In{edef2}, \dots , \In{edefN}. И импортирует определения
из модулей \In{Old1}, \In{Old2}, и т.д.,
определения написаны в скобках за ключевыми словами \In{import} и
именами модулей.

\subsubsection{Типы}

Тип определяется с помощью:

\begin{itemize}

\item Перечисления альтернатив через \In{|} 
        
\begin{code}
data Type = Alt1 | Alt2 | ... | AltN
\end{code}

Эту операцию называют \Types{сумма типов} \emph{суммой} типов.

\item Составления сложного типа из подтипов, пишем
        конструктор первым, затем через пробел подтипы:
   
\begin{code}
data Type = Name  Sub1  Sub2  ...  SubN
\end{code}

Эту операцию называют \Types{произведение типов} \emph{произведением} типов.

Есть одно исключение: если тип состоит из двух подтипов,
мы можем дать конструктору символьное (а не буквенное) имя,
но оно должно начинаться с двоеточия \In{:}, как в случае списка,
например, можно делать такие определения типов:

\begin{code}
data Type = Sub1 :+ Sub2 
data Type = Sub1 :| Sub2 
\end{code}


\item Комбинации суммы и произведения типов:

\begin{code}
data Type = Name1  Sub11  Sub12  ...  Sub1N
          | Name2  Sub21  Sub22  ...  Sub2M
          ...
          | NameK  SubK1  SubK2  ...  SubKP
\end{code}

\end{itemize}

Такие типы называют \Types{алгебраические типы данных}
\emph{алгебраическими типами данных}.
С помощью типов мы определяем основные понятия и способы
их комбинирования.

\subsubsection{Значения}

Как это ни странно, нам встретилась лишь одна операция
создания значений: \emph{определение синонима}. Она пишется так

\begin{code}
name x1  x2 ... xN = Expr1
name x1  x2 ... xN = Expr2
name x1  x2 ... xN = Expr3
\end{code}

Слева от знака равно стоит составное имя, а справа от знака
равно некоторое выражение, построенное согласно типам.
Разные комбинации имени \In{name} с параметрами определяют
разные уравнения для синонима \In{name}.

Также мы видели символ \In{_}, который означает \Quote{всё, что угодно}
на месте аргумента. А также мы  увидели, как с помощью переменных
можно перетаскивать значения из аргументов в результат.


\subsubsection{Классы типов}

Нам встретилась одна конструкция определения классов типов:

\begin{code}
class Name a where
    method1 :: a -> ...
    method2 :: a -> ...
    ...
    methodN :: a -> ...
\end{code}

\subsubsection{Экземпляры классов типов}

Нам встретилась одна конструкция определения экземпляров
классов типов:

\begin{code}
instance Name Type where
    method1 x1 ... xN = ...
    method2 x1 ... xM = ...
    ...
    methodN x1 ... xP = ...
\end{code}
\subsubsection{Типы, значения и классы типов}

Каждое значение имеет тип.
Значение \In{v} имеет тип \In{T} на Haskell: 

\begin{code}
v :: T
\end{code}


Функциональный тип обозначается стрелкой: \In{a -> b}

\begin{code}
fun :: a -> b
\end{code}

Тип значения может иметь контекст, он говорит о том, что
параметр должен принадлежать классу типов:

\begin{code}
fun1 :: С a                 => a -> a
fun2 :: (C1 a, C2, ..., CN) => a -> a
\end{code}

\subsubsection{Суперклассы}

Также контекст может быть и у классов, запись

\begin{code}
class A a => B a where
    ...
\end{code}

Означает, что класс \In{B} целиком содержится в \In{A},
и перед тем как объявлять экземпляр для класса \In{B},
необходимо определить экземпляр для класса \In{A}. 
При этом класс \In{A} называют суперклассом для \In{B}.

\section{Двумерный синтаксис}

Наверное вы обратили внимание на то, что в Haskell
нет разделителей строк и дополнительных скобок, которые
бы указывали границы определения классов или функций. 
Компилятор Haskell ориентируется по переносам строки и 
знакам табуляции.

Так если мы пишем в классе:

\begin{code}
class Eq a where
    (==) :: a -> a -> a
    (/=) :: a -> a -> a
\end{code}

По отступам за первой строкой определения компилятор
понимает, что класс содержит два метода. Если бы мы написали:

\begin{code}
class Eq a where
    (==) :: a -> a -> a
(/=) :: a -> a -> a
\end{code}

То смысл был бы совсем другим. Теперь мы определяем
класс \In{Eq} с одним методом \In{==} и указываем
тип некоторого значения \In{(/=)}. Основное правило
такое: конструкции, расположенные на одном уровне, 
выравниваются с помощью табуляции. Чем правее находится определение,
тем глубже оно вложено в какую-нибудь специальную
конструкцию. Пока нам встретилось лишь несколько специальных
конструкций, но дальше появятся и другие. 

\section{Краткое содержание}

Итак подведём итоги: у нас есть две операции для определения типов
(сумма и произведение) и по одной для значений (синонимы), 
классов типов и экземпляров. А также
бесконечное множество их комбинаций, из которых и состоит
увлекательный мир Haskell. Конечно не только из них, есть
нюансы, синтаксический сахар, расширения языка. Об этом
и многом другом мы узнаем из этой книги. 

Интересно, что в Haskell, несмотря на обилие 
конструкций и библиотек, ты чувствуешь, что за ними стоит
нечто из мира науки, мира чистого знания. Ты не просто учишься 
пользоваться определёнными функциями или классами, а узнаёшь
что-то новое и красивое.

\section{Упражнения}

Потренируйтесь в описаниях в рамках системы типов.
Вы определяете базовые понятия и способы их комбинирования.
У вас есть три операции:

\begin{itemize}

\item Сумма типов \In{data T = A1 | A2}. Перечисление альтернатив
            
\item Произведение типов \In{data T = S S1 S2}. 
        Этим мы говорим, что понятие состоит из нескольких.

\item Взятие в список \In{[T]}. Обозначает множественное
        число, элементов типа \In{T} их может быть несколько.

\end{itemize}

Опишите что-либо: комнату, дорогу, город, человека,
главу из книги, математическую теорию, всё что угодно.

Ниже приведён пример для понятий из этой главы:

\begin{code}
data Program = Programm ProgramType [Module]
data ProgramType = Executable | Library

data Module = Module [Definition]

data Definition = Definition DefinitionType Element
data DefinitionType = Export | Inner

data Element = ET Type | EV Value | EC Class | EI Instance

data Type     = Type String
data Value    = Value String
data Class    = Class String
data Instance = Instance String
\end{code}

После того как вы закончите с описанием, подумайте,
какие производные связи могли бы вас заинтересовать.
Какие функции вам бы хотелось определить в
этом описании. Выпишите их типы без определений, например так:


\begin{code}
-- Все оьъявления типов в модуле
getTypes :: Module -> [Type]

-- Провести редукцию значения:
reduce :: Value -> Program -> Value

-- Проверить типы:
checkTypes :: Program -> Bool

-- Заменить все определения в модуле на новые
setDefinitions    :: Module -> [Definition] -> Module

-- Упорядочить определения по какому-лбо принципу
orderDefinitions :: [Definition] -> [Definition]
\end{code}

Подумайте: если у вас есть все эти функции, какие производные
значения могли бы вам сказать что-нибудь интересное.

