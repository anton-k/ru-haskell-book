\chapter{Первая программа}

Я вот говорю-говорю, а вдруг я вас обманываю, и ничего этого нет. В этой
главе мы перейдём к программированию и запустим нашу первую программу в
Haskell. Будет много примеров, на которых мы закрепим наши знания.

\section{Интерпретатор}

Для запуска кода мы будем пользоваться приложением GHC (Glorious Glasgow
Haskell Compiler) наиболее развитой системой интерпретации Haskell
программ. В GHC есть компилятор \In{ghc} и интерпретатор \In{ghci}. Пока
мы будем пользоваться лишь интерпретатором. Если вы не знаете как
установить \In{ghc} загляните в приложение. Также нам понадобится
текстовый редактор с подсветкой синтаксиса. Подсветка синтаксиса для
Haskell по умолчанию есть в редакторах Vim, Emacs, gedit, geany, yi.
Есть IDE для Haskell Leksah. Мы будем писать модули в файлах и загружать
их в интерпретатор. Если вы не знаете продвинутых текстовых редакторов
вроде Vim или Emacs, лучше всего будет начать с gedit.

Интерпретатор позволяет загружать модуль с определениями и набирать
значения в командной строке. Мы набираем значение, а интерпретатор
редуцирует его и показывает нам ответ. Интерпретатор запускается
командой \In{ghci} в терминале. Определения из модуля могут быть
загружены в интерпретатор двумя способами, либо при запуске
интерпретатора командой \In{ghci ИмяМодуля.hs} либо в самом
интерпретаторе командой \In{:l ИмяМодуля.hs}.

Рассмотрим некоторые полезные команды интерпретатора:

\begin{description}
\item[\In{:?}]
Выводит на экран список доступных команд
\item[\In{:t Expression}]
Возвращает тип выражения.
\item[\In{:set +t}]
После выполнения команды интерпретатор будет выводить на экран не только
результат вычисления выражения, но и его тип.
\item[\In{:set +s}]
После выполнения команды интерпретатор будет выводить на экран не только
результат вычисления выражения, но и статистику вычислений.
\item[\In{:l ИмяМодуля}]
Загружает модуль в интерпретатор.
\item[\In{:cd Директория}]
Перейти в данную директорию.
\item[\In{:r}]
Перезагружает, последний загруженный модуль. Этой командой можно
пользоваться после внесения в модуль изменений.
\item[\In{:q}]
Выход из интерпретатора.
\end{description}

\section{У-вей}

Согласно даосам основной принцип жизни заключается в недеянии (у-вей).
Всё происходит естественно и словно само собой. Давайте создадим модуль
который ничего не делает. Создадим пустой модуль и загрузим его в
интерпретатор.


\begin{code}
module Empty where

import Prelude()
\end{code}

Зачем мы написали \In{import Prelude()}? Этой фразой мы говорим, что не
хотим ничего импортировать из модуля \In{Prelude}. По умолчанию в любой
модуль загружается модуль \In{Prelude}, который содержит много полезных
определений. К примеру там определяется тип \In{Bool}, списки и функции
для них, символы, классы типов для сравнения на равенство и печати
значений и много, много других определений. В первых главах я хочу
сделать акцент на самом языке Haskell, а не на производных выражениях,
поэтому пока мы будем в явном виде загружать из модуля \In{Prelude} лишь
самые необходимые определения.

Сохраним модуль в файле \In{Empty.hs}, сделаем директорию модуля текущей
и запустим интерпретатор командой \In{ghci Empty} (имя расширения можно
не писать). Также можно просто запустить интерпретатор командой
\In{ghci}, переключиться на директорию командой \In{:cd} и загрузить
модуль командой \In{:l Empty}.


\begin{verbatim}
$ ghci
GHCi, version 7.4.1: http://www.haskell.org/ghc/  :? for help
Loading package ghc-prim ... linking ... done.
Loading package integer-gmp ... linking ... done.
Loading package base ... linking ... done.
Prelude> :cd ~/haskell-notes/code/ch-2/
Prelude> :l Empty.hs 
[1 of 1] Compiling Empty            ( Empty.hs, interpreted )
Ok, modules loaded: Empty.
*Empty> 
\end{verbatim}

Слева от знака приглашения к вводу \In{>} отображаются загруженные в
интерпретатор модули. По умолчанию загружается модуль \In{Prelude}.
После выполнения команды \In{:l} мы видим, что \In{Prelude} сменилось на
\In{Empty}.

Теперь давайте потренируемся перезагружать модули. Давайте изменим наш
модуль, сделаем его не таким пустым, убрав последние две скобки от
модуля \In{Prelude} в директиве \In{import}. Теперь сохраним изменения и
выполним команду \In{:r}.


\begin{code}
*Empty> :r
[1 of 1] Compiling Empty            ( Empty.hs, interpreted )
Ok, modules loaded: Empty.
*Empty>
\end{code}

Завершим сессию интерпретатора командой \In{:q}.


\begin{code}
*Empty> :q
Leaving GHCi.
\end{code}

Внешние модули должны находится в текущей директории. Давайте
потренируемся с подключением определений из внешних модулей. Создадим
модуль близнец модуля \In{Empty.hs}:


\begin{code}
module EmptyEmpty where

import Prelude()
\end{code}

И сохраним его в той же директории, что и модуль \In{Empty}, теперь мы
можем включить все определения из модуля \In{EmptyEmpty}:


\begin{code}
module Empty where

import EmptyEmpty
\end{code}

Когда у нас будет много модулей мы можем разместить их по директориям.
Создадим в одной директории с модулем \In{Empty} директорию \In{Sub}, а
в неё поместим копию модуля \In{Empty}. Существует одна тонкость:
поскольку модуль находится в поддиректории, для того чтобы он стал виден
из текущей директории, необходимо дописать через точку имя директории в
которой он находится:


\begin{code}
module Sub.Empty where
\end{code}

Теперь мы можем загрузить этот модуль из исходного:


\begin{code}
module Empty where

import EmptyEmpty
import Sub.Empty
\end{code}

Обратите внимание на то, что мы приписываем к модулю в поддиректории
\In{Sub} имя поддиректории. Если бы он был заложен в ещё одной
директории, то мы написали бы через точку имя и этой поддиректории:


\begin{code}
module Empty where

import Sub1.Sub2.Sub3.Sub4.Empty
\end{code}

\section{Логические значения}

Пустой модуль это хорошо, но слишком скучно. Давайте перепишем
объявленные в этой главе определения в модуль, загрузим его в
интерпретатор и понабираем значения.

Начнём с логических операций. Давайте не будем переопределять \In{Bool},
\In{Show} и \In{Eq}, а просто возьмём их из \In{Prelude}:


\begin{code}
module Logic where

import Prelude(Bool(..), Show(..), Eq(..))
\end{code}

Две точки в скобках означают ``все конструкторы'' (в случае типа) и
``все методы'' (в случае класса типа). Строчку


\begin{code}
import Prelude(Bool(..), Show(..), Eq(..))
\end{code}

Следует читать так: Импортируй из модуля \In{Prelude} тип \In{Bool} и
все его конструкторы и классы \In{Show} и \In{Eq} со всеми их методами.
Если бы мы захотели импортировать только конструктор \In{True}, мы бы
написали \In{Bool(True)}, а если бы мы захотели импортировать лишь имя
типа, мы бы написали просто \In{Bool} без скобок.

Сначала выпишем в модуль наши синонимы:


\begin{code}
module Logic where

import Prelude(Bool(..), Show(..), Eq(..))
  
true :: Bool 
true = True

false :: Bool
false = False

not :: Bool -> Bool
not True  = False
not False = True

and :: Bool -> Bool -> Bool
and False  _  = False
and True   x  = x

or  :: Bool -> Bool -> Bool
or True   _ = True
or False  x = x

xor :: Bool -> Bool -> Bool
xor a b = or (and (not a) b) (and a (not b))

ifThenElse :: Bool -> a -> a -> a
ifThenElse True   t  _ = t
ifThenElse False  _  e = e
\end{code}

Теперь сохраним модуль и загрузим его в интерпретатор. Для наглядности
мы установим флаг \In{+t}, при этом будет возвращено не только значение,
но и его тип. Понабираем разные комбинации значений:


\begin{code}
*Logic> :l Logic
[1 of 1] Compiling Logic            ( Logic.hs, interpreted )
Ok, modules loaded: Logic.
*Logic> :set +t
*Logic> not (and true False)
True
it :: Bool
*Logic> or (and true true) (or False False)
True
it :: Bool
*Logic> xor (not True) (False)
False
it :: Bool
*Logic> ifThenElse (or true false) True False
True
it :: Bool
\end{code}

Разумеется в Haskell уже определены логические операции, здесь мы просто
тренировались. Они называются \In{not}, \In{(&&)}, \In{||}. Операция
\In{xor} это то же самое, что и \In{(/=)}. Для \In{Bool} определён
экземпляр класса \In{Eq}. Также в Haskell есть конструкция ветвления она
пишется так:


\begin{code}
x = if cond then t else e
\end{code}

Слова \In{if}, \In{then} и \In{else} -- ключевые. \In{cond} имеет тип
\In{Bool}, а \In{t} и \In{e} одинаковый тип.

В коде программы обычно пишут так:


\begin{code}
x = if a > 3 
    then "Hello"
    else (if a < 0
          then "Hello"
          else "Bye")
\end{code}

Отступы обязательны.

Давайте загрузим в интерпретатор модуль \In{Prelude} и наберём те же
выражения стандартными функциями:


\begin{code}
*Logic> :m Prelude
Prelude> not (True && False)
True
it :: Bool
Prelude> (True && True) || (False || False)
True
it :: Bool
Prelude> not True /= False
False
it :: Bool
Prelude> if (True || False) then True else False
True
it :: Bool
\end{code}

Бинарные операции с символьными именами пишутся в инфиксной форме,
т.е.\textasciitilde{}между аргументами как в \In{a && b} или \In{a + b}.
Значение с буквенным именем также можно писать в инфиксной форме, для
этого оно заключается в апострофы, например \In{a `and` b} или
\In{a `plus` b}. Апострофы обычно находятся на одной кнопке с буквой
``ё''. Также символьные функции можно применять в префиксной форме,
заключив их в скобки, например \In{(&&) a b} и \In{(+) a b}. Попробуем в
интерпретаторе:


\begin{code}
Prelude> True && False
False
it :: Integer
Prelude> (&&) True False
False
it :: Bool
Prelude> let and a b = a && b
and :: Bool -> Bool -> Bool
Prelude> and True False
False
it :: Bool
Prelude> True `and` False
False
it :: Bool
\end{code}

Обратите внимание на строчку \In{let and a b = a && b}. В ней мы
определили синоним в интерпретаторе. Сначала мы пишем ключевое слово
\In{let} затем обычное определение синонима, как в программе. Но не
совсем обычное синоним должен быть однострочным. У нас не получится
например определить такой синоним как \In{not}. Потому что в нём два
уравнения.

\section{Класс Show. Строки и символы}

Мы набираем в интерпретаторе какое-нибудь сложное выражение, или
составной синоним, интерпретатор проводит редукцию и выводит ответ на
экран. Откуда интерпретатор знает как отображать значения типа
\In{Bool}? Внутри интерпретатора вызывается метод класса \In{Show},
который переводит значение в строку. И затем мы видим на экране
ответ.\\Для типа \In{Bool} экземпляр класса \In{Show} уже определён,
поэтому интерпретатор знает как его отображать.

Обратите внимание на эту особенность языка, вид значения определяется
пользователем, в экземпляре класса \In{Show}. Из соображений наглядности
вид значения может сильно отличаться от его внутреннего представления.

В этом разделе мы рассмотрим несколько примеров с классом \In{Show}, но
перед этим мы поговорим о строках и символах в языке Haskell.

\subsection{Строки и символы}

Посмотрим в интерпретаторе что из себя представляют строки (тип
\In{String}), для этого мы воспользуемся командой \In{:i} (сокращение от
\In{:info}):


\begin{code}
Prelude> :i String
type String = [Char] 	-- Defined in `GHC.Base'
\end{code}

Интерпретатор показал определение типа и в комментариях указал в каком
модуле тип определён. В этом определении мы видим новое ключевое слово
\In{type}. До этого для определения типов нам встречалось лишь слово
\In{data}. Ключевое слово \In{type} определяет синоним типа. При этом мы
не вводим новый тип, мы лишь определяем для него псевдоним. \In{String}
является синонимом для списка значений типа \In{Char}. Тип \In{Char}
представляет символы. Итак строка -- это список символов. В Haskell
символы пишутся в ординарных кавычках, а строки в двойных:


\begin{code}
Prelude> ['H','e','l','l','o']
"Hello"
it :: [Char]
Prelude> "Hello"
"Hello"
it :: [Char]
Prelude> '+'
'+'
it :: Char
\end{code}

Для обозначения перехода на новую строку используется специальный символ
\In{}\verb!\!\In{n}. Если строка слишком длинная и не помещается на
одной строке, то её можно перенести так:


\begin{code}
str = "My long long long long \
        \long long string"
\end{code}

Перенос осуществляется с помощью комбинации следующих друг за другом
обратных слэшей.

Нам понадобится функция конкатенации списков \In{(++)}, она определена в
\In{Prelude}, с её помощью мы будем объединять строки:


\begin{code}
Prelude> :t (++)
(++) :: [a] -> [a] -> [a]
Prelude> "Hello" ++ [' '] ++ "World"
"Hello World"
it :: [Char]
\end{code}

\subsection{Пример: Отображение дат и времени}

Приведём, пример в котором отображаемое значение не совпадает с видом
значения в коде. Мы отобразим значения из мира календаря. Для начала
давайте сохраним определения в отдельном модуле:


\begin{code}
module Calendar where

import Prelude (Int, Char, String, Show(..), (++))

-- Дата
data Date = Date Year Month Day

-- Год
data Year  = Year Int       -- Int это целые числа

-- Месяц
data Month  = January    | February   | March    | April          
            | May        | June       | July     | August   
            | September  | October    | November | December

data Day = Day Int

-- Неделя
data Week  = Monday     | Tuesday   | Wednesday 
           | Thursday   | Friday    | Saturday     
           | Sunday   

-- Время
data Time = Time Hour Minute Second

data Hour   = Hour   Int    -- Час
data Minute = Minute Int    -- Минута
data Second = Second Int    -- Секунда
\end{code}

Теперь сохраним наш модуль под именем \In{Calendar.hs} и загрузим в
интерпретатор:


\begin{code}
Prelude> :l Calendar
[1 of 1] Compiling Calendar         ( Calendar.hs, interpreted )
Ok, modules loaded: Calendar.
*Calendar> Monday

<interactive>:3:1:
    No instance for (Show Week)
      arising from a use of `System.IO.print'
    Possible fix: add an instance declaration for (Show Week)
    In a stmt of an interactive GHCi command: System.IO.print it
\end{code}

Смотрите мы попытались распечатать значение \In{Monday}, но в ответ
получили ошибку. В ней интерпретатор сообщает нам о том, что для типа
\In{Week} не определён экземпляр класса \In{Show} и он не знает как его
распечатывать. Давайте подскажем ему. Обычно дни недели в календарях
печатают не полностью, в имя попадают лишь три первых буквы:


\begin{code}
instance Show Week where
    show Monday     = "Mon"
    show Tuesday    = "Tue"
    show Wednesday  = "Wed"
    show Thursday   = "Thu"  
    show Friday     = "Fri" 
    show Saturday   = "Sat"
    show Sunday     = "Sun"
\end{code}

Отступы перед \In{show} обязательны, но выравнивание по знаку равно не
обязательно, мне просто нравится так писать. По отступам компилятор
понимает, что все определения относятся к определению \In{instance}.
Теперь запишем экземпляр в модуль, сохраним, и перезагрузим в
интерпретатор:


\begin{code}
*Calendar> :r
[1 of 1] Compiling Calendar         ( Calendar.hs, interpreted )
Ok, modules loaded: Calendar.
*Calendar> Monday
Mon
it :: Week
*Calendar> Sunday
Sun
it :: Week
\end{code}

Теперь наши дни отображаются. Я выпишу ещё один пример экземпляра для
\In{Time}, а остальные достанутся вам в качестве упражнения.


\begin{code}
instance Show Time where
    show (Time h m s) = show h ++ ":" ++ show m ++ ":" ++ show s

instance Show Hour where
    show (Hour h) = addZero (show h)

instance Show Minute where
    show (Minute m) = addZero (show m)

instance Show Second where
    show (Second s) = addZero (show s)

addZero :: String -> String
addZero (a:[]) = '0' : a : []
addZero as     = as
\end{code}

Функцией \In{addZero} мы добавляем ноль в начало строки, в том случае
если число однозначное, также в этом определении мы воспользовались тем,
что для типа целых чисел \In{Int} экземпляр \In{Show} уже определён.
Проверим в интерпретаторе:


\begin{code}
*Calendar> Time (Hour 13) (Minute 25) (Second 2)
13:25:02
it :: Time
\end{code}

\section{Автоматический вывод экземпляров классов типов}

Для некоторых стандартных классов экземпляры классов типов могут быть
выведены автоматически. Это делается с помощью директивы \In{deriving}.
Она пишется сразу после объявления типа. Например так мы можем
определить тип и экземпляры для классов \In{Show} и \In{Eq}:


\begin{code}
data T = A | B | C
    deriving (Show, Eq)
\end{code}

Отступ за \In{deriving} обязателен, после ключевого слова в скобках
указываются классы, которые мы хотим вывести.

\section{Арифметика}

В этом разделе мы обсудим основные арифметические операции. В Haskell
много стандартных классов, которые группируют различные типы операций,
есть класс для сравнения на равенство, отдельный класс для сравнения на
больше/меньше, класс для умножения, класс для деления, класс для
упорядоченных чисел, и много других. Зачем такое изобилие классов?

Каждый из классов отвечает независимой группе операций. Есть много
объектов, которые можно только складывать, но нельзя умножать или
делить. Есть объекты, для которых сравнение на равенство имеет смысл, а
сравнение на больше/меньше -- нет.

Для иллюстрации мы воспользуемся числами Пеано, у них компактное
определение, всего два конструктора, которых тем не менее достаточно для
описания множества натуральных чисел:


\begin{code}
module Nat where

data Nat = Zero | Succ Nat
    deriving (Show, Eq, Ord)
\end{code}

Конструктор \In{Zero} указывает на число ноль, а \In{(Succ n)} на число
следующее за данным числом \In{n}. В последней строчке мы видим новый
класс \In{Ord}, этот класс содержит операции сравнения на больше/меньше:


\begin{code}
Prelude> :i Ord
class (Eq a) => Ord a where
  compare :: a -> a -> Ordering
  (<) :: a -> a -> Bool
  (>=) :: a -> a -> Bool
  (>) :: a -> a -> Bool
  (<=) :: a -> a -> Bool
  max :: a -> a -> a
  min :: a -> a -> a
\end{code}

Тип \In{Ordering} кодирует результаты сравнения:


\begin{code}
Prelude> :i Ordering
data Ordering = LT | EQ | GT 	-- Defined in GHC.Ordering
\end{code}

Он содержит конструкторы, соответствующие таким понятиям как меньше,
равно и больше.

\subsection{Класс Eq. Сравнение на равенство}

Вспомним определение класса \In{Eq}:


\begin{code}
class Eq a where
    (==) :: a -> a -> Bool
    (/=) :: a -> a -> Bool

    a == b = not (a /= b)
    a /= b = not (a == b)
\end{code}

Появились две детали, о которых я умолчал в предыдущей главе. Это две
последние строчки. В них мы видим определение \In{==} через \In{/=} и
наоборот. Это определения методов по умолчанию. Такие определения дают
нам возможность определять не все методы класса, а лишь часть основных,
а все остальные мы получим автоматически из определений по умолчанию.

Казалось бы почему не оставить в классе \In{Eq} один метод а другой
метод определить в виде отдельной функции:


\begin{code}
class Eq a where
    (==) :: a -> a -> Bool

(/=) :: Eq a => a -> a -> Bool
a /= b = not (a == b)
\end{code}

Так не делают по соображениям эффективности. Есть типы для которых проще
вычислить \In{/=} чем \In{==}. Тогда мы определим тот метод, который нам
проще вычислять и второй получим автоматически.

Набор основных методов, через которые определены все остальные называют
\emph{минимальным полным определением} (minimal complete definition)
класса. В случае класса \In{Eq} это метод \In{==} или метод \In{/=}.

Мы уже вывели экземпляр для \In{Eq}, поэтому мы можем пользоваться
методами \In{==} и \In{/=} для значений типа \In{Nat}:


\begin{code}
*Calendar> :l Nat
[1 of 1] Compiling Nat              ( Nat.hs, interpreted )
Ok, modules loaded: Nat.
*Nat> Zero == Succ (Succ Zero)
False
it :: Bool
*Nat> Zero /= Succ (Succ Zero)
True
it :: Bool
\end{code}

\subsection{Класс Num. Сложение и умножение}

Сложение и умножение определены в классе \In{Num}. Посмотрим на его
определение:


\begin{code}
*Nat> :i Num
class (Eq a, Show a) => Num a where
  (+) :: a -> a -> a
  (*) :: a -> a -> a
  (-) :: a -> a -> a
  negate :: a -> a
  abs :: a -> a
  signum :: a -> a
  fromInteger :: Integer -> a
  	-- Defined in GHC.Num
\end{code}

Методы \In{(+)}, \In{(*)}, \In{(-)} в представлении не нуждаются, метод
\In{negate} является унарным минусом, его можно определить через
\In{(-)} так:


\begin{code}
negate x = 0 - x
\end{code}

Метод \In{abs} является модулем числа, а метод \In{signum} возвращает
знак числа, метод \In{fromInteger} позволяет создавать значения данного
типа из стандартных целых чисел \In{Integer}.

Этот класс устарел, было бы лучше сделать отельный класс для сложения и
вычитания и отдельный класс для умножения. Также контекст класса, часто
становится помехой. Есть объекты, которые нет смысла печатать но, есть
смысл определить на них сложение и умножение. Но пока в целях
совместимости с уже написанным кодом, класс \In{Num} остаётся прежним.

Определим экземпляр для чисел Пеано, но давайте сначала разберём функции
по частям.

\subsubsection{Сложение}

Начнём со сложения:


\begin{code}
instance Num Nat where
    (+) a Zero     = a
    (+) a (Succ b) = Succ (a + b)
\end{code}

Первое уравнение говорит о том, что если второй аргумент равен нулю, то
мы вернём первый аргумент в качестве результата. Во втором уравнении мы
``перекидываем'' конструктор \In{Succ} из второго аргумента за пределы
суммы. Схематически вычисление суммы можно представить так:

\[\textbf{3+2} \rightarrow 1+\textbf{(3+1)} \rightarrow 1+(1+\textbf{(3+0)})\]

\[1+(1+\textbf{3}) \rightarrow 1+(1+(1+(1+(1+0)))) \rightarrow 5\]

Все наши числа имеют вид $0$ или $1+n$, мы принимаем на вход два числа в
таком виде и хотим в результате составить число в этом же виде, для
этого мы последовательно перекидываем \$$(1+)$ в начало выражения из
второго аргумента.

\subsubsection{Вычитание}

Операция отрицания не имеет смысла, поэтому мы воспользуемся специальной
функцией \In{error :: String -> a}, она принимает строку с сообщением об
ошибке, при её вычислении программа остановится с ошибкой и сообщение
будет выведено на экран.


\begin{code}
    negate _ = error "negate is undefined for Nat"
\end{code}

\subsubsection{Умножение}

Теперь посмотрим на умножение:


\begin{code}
    (*) a Zero     = Zero
    (*) a (Succ b) = a + (a * b)     
\end{code}

В первом уравнении мы вернём ноль, если второй аргумент окажется нулём,
а во втором мы за каждый конструктор \In{Succ} во втором аргументе
прибавляем к результату первый аргумент. В итоге, после вычисления
\In{a * b} мы получим аргумент \In{a} сложенный \In{b} раз. Это и есть
умножение. При этом мы воспользовались операцией сложения, которую
только что определили. Посмотрим на схему вычисления:

\[\textbf{3*2} \rightarrow 3+\textbf{(3*1)} \rightarrow 
    3+(3+\textbf{(3*0)}) \rightarrow 3+\textbf{(3+0)} 
    \rightarrow \textbf{3+3} \rightarrow\]

\[1+\textbf{(3+2)} \rightarrow 1+(1+\textbf{(3+1)}) \rightarrow
    1+(1+(1+\textbf{(3+0)})) \rightarrow\]

\[1+(1+1+\textbf{3}) \rightarrow 1+(1+(1+(1+(1+(1+0))))) \rightarrow 6\]

\subsubsection{Операции abs и signum}

Поскольку числа у нас положительные, то методы \In{abs} и \In{signum}
почти ничего не делают:


\begin{code}
    abs    x    = x 
    signum Zero = Zero
    signum _    = Succ Zero
\end{code}

\subsubsection{Перегрузка чисел}

Остался последний метод \In{fromInteger}. Он конструирует значение
нашего типа из стандартного:


\begin{code}
    fromInteger 0 = Zero
    fromInteger n = Succ (fromInteger (n-1))
\end{code}

Зачем он нужен? Попробуйте узнать тип числа \In{1} в интерпретаторе:


\begin{code}
*Nat> :t 1
1 :: (Num t) => t
\end{code}

Интерпретатор говорит о том, тип значения \In{1} является некоторым
типом из класса \In{Num}. В Haskell обозначения для чисел перегружены.
Когда мы пишем \In{1} на самом деле мы пишем
\In{(fromInteger (1::Integer))}. Поэтому теперь мы можем не писать
цепочку \In{Succ}-ов, а воспользоваться методом \In{fromInteger}, для
этого сохраним определение экземпляра для \In{Num} и загрузим
обновлённый модуль в интерпретатор:


\begin{code}
[1 of 1] Compiling Nat              ( Nat.hs, interpreted )
Ok, modules loaded: Nat.
*Nat> 7 :: Nat
Succ (Succ (Succ (Succ (Succ (Succ (Succ Zero))))))
*Nat> (2 + 2) :: Nat
Succ (Succ (Succ (Succ Zero)))
*Nat> 2 * 3 :: Nat
Succ (Succ (Succ (Succ (Succ (Succ Zero)))))
\end{code}

Вы можете убедиться насколько гибкими являются числа в Haskell:


\begin{code}
*Nat> (1 + 1) :: Nat
Succ (Succ Zero)
*Nat> (1 + 1) :: Double
2.0
*Nat> 1 + 1
2
\end{code}

Мы выписали три одинаковых выражения и получили три разных результата,
меняя объявление типов. В последнем выражении тип был приведён к
\In{Integer}. Это поведение интерпретатора по умолчанию. Если мы
напишем:


\begin{code}
*Nat> let q = 1 + 1
*Nat> :t q
q :: Integer
\end{code}

Мы видим, что значение \In{q} было переведено в \In{Integer}, это
происходит лишь в интерпретаторе, если такая переменная встретится в
программе и компилятор не сможет определить её тип из контекста,
произойдёт ошибка проверки типов, компилятор скажет, что он не смог
определить тип. Помочь компилятору можно, добавив объявление типа с
помощью конструкции \In{(v :: T)}.

Посмотрим ещё раз на определение экземпляра \In{Num} для \In{Nat}
целиком:


\begin{code}
instance Num Nat where
    (+) a Zero     = a
    (+) a (Succ b) = Succ (a + b)

    (*) a Zero     = Zero
    (*) a (Succ b) = a + (a * b)   

    fromInteger 0 = Zero
    fromInteger n = Succ (fromInteger (n-1))

    abs    x    = x 
    signum Zero = Zero
    signum _    = Succ Zero

    negate _ = error "negate is undefined for Nat"
\end{code}

\subsection{Класс Fractional. Деление}

Деление определено в классе \In{Fractional}:


\begin{code}
*Nat>:m Prelude
Prelude> :i Fractional
class Num a => Fractional a where
  (/) :: a -> a -> a
  recip :: a -> a
  fromRational :: Rational -> a
  	-- Defined in `GHC.Real'
instance Fractional Float -- Defined in `GHC.Float'
instance Fractional Double -- Defined in `GHC.Float'
\end{code}

Функция \In{recip}, это аналог \In{negate} для \In{Num}. Она делит
единицу на данное число. Функция \In{fromRational} строит число данного
типа из дробного числа. Если мы пишем \In{2}, то к нему подспудно будет
применена функция \In{fromInteger}, а если \In{2.0}, то будет применена
функция \In{fromRational}.

\subsection{Стандартные числа}

В этом подразделе мы рассмотрим несколько стандартных типов для чисел в
Haskell. Все эти числа являются экземплярами основных численных классов.
Тех которые мы рассмотрели, и многих-многих других.

\subsubsection{Целые числа}

В Haskell предусмотрено два типа для целых чисел. Это \In{Integer} и
\In{Int}. Чем они отличаются? Значения типа \In{Integer} не ограничены,
мы можем проводить вычисления с очень-очень-очень большими числами, если
памяти на нашем компьютере хватит. Числа из типа \In{Int} ограничены.
Каждое число занимает определённый размер в памяти компьютера. Диапазон
значений для \In{Int} составляет от $-2^{29}$ до $2^{29}-1$. Вычисления
с \In{Int} более эффективны.

\subsubsection{Действительные числа}

\Types{Double, Float, Rational, действительные числа} Действительные
числа бывают дробными (тип \In{Rational}), с ординарной точностью
\In{Float} и с двойной точностью \In{Double}. Числа из типа \In{Float}
занимают меньше места, но они не такие точные как \In{Double}. Если вы
сомневаетесь чем пользоваться, выбирайте \In{Double}, обычно \In{Float}
используется только там, где необходимо хранить огромные массивы чисел.
В этом случае мы экономим много памяти.

\subsubsection{Преобразование численных типов}

Во многих языках программирования при сложении или умножении чисел
разных типов проводится автоматическое приведение типов. Обычно целые
числа становятся действительными, \In{Float} превращается в \In{Double}
и так далее. Это противоречит строгой типизации, поэтому в Haskell этого
нет:


\begin{code}
Prelude> (1::Int) + (1::Double)

<interactive>:2:13:
    Couldn't match expected type `Int' with actual type `Double'
    In the second argument of `(+)', namely `(1 :: Double)'
    In the expression: (1 :: Int) + (1 :: Double)
    In an equation for `it': it = (1 :: Int) + (1 :: Double)
\end{code}

Любое преобразование типов контролируется пользователем. Мы должны
вызвать специальную функцию.

\textbf{От целых к действительным:} Часто возникает необходимость
приведения целых чисел к действительным при делении. Для этого можно
воспользоваться функцией: \In{fromIntegral}


\begin{code}
Prelude> :i fromIntegral
fromIntegral :: (Integral a, Num b) => a -> b
  	-- Defined in `GHC.Real'
\end{code}

Определим функцию поиска среднего между двумя целыми числами:


\begin{code}
meanInt :: Int -> Int -> Double
meanInt a b = fromIntegral (a + b) / 2
\end{code}

В этой функции двойка имеет тип \In{Double}. Обратите внимание на
скобки: составной синоним всегда притягивает аргументы сильнее чем
бинарная операция.

\textbf{От действительных к целым:} В этом нам поможет класс
\In{RealFrac}. Методы говорят сами за себя:


\begin{code}
Prelude GHC.Float> :i RealFrac
class (Real a, Fractional a) => RealFrac a where
  properFraction :: Integral b => a -> (b, a)
  truncate :: Integral b => a -> b
  round :: Integral b => a -> b
  ceiling :: Integral b => a -> b
  floor :: Integral b => a -> b
  	-- Defined in `GHC.Real'
instance RealFrac Float -- Defined in `GHC.Float'
instance RealFrac Double -- Defined in `GHC.Float'
\end{code}

Метод \In{properFraction} отделяет целую часть числа от дробной:


\begin{code}
properFraction :: Integral b => a -> (b, a)
\end{code}

Для того, чтобы вернуть сразу два значения используется кортеж (кортежи
пишутся в обычных скобках значения следуют через запятую):


\begin{code}
Prelude> properFraction 2.5
(2,0.5)
\end{code}

\Types{кортежи} Для пар (кортеж, состоящий из двух элементов) определены
две удобные функции извлечения элементов, их смысл можно понять по одним
лишь типам:


\begin{code}
fst :: (a, b) -> a
snd :: (a, b) -> b
\end{code}

Проверим:


\begin{code}
Prelude> let x = properFraction 2.5
Prelude> (fst x, snd x)
(2, 0.5)
\end{code}

Мы бы и сами могли определить такие функции:


\begin{code}
fst :: (a, b) -> a
fst (a, _) = a 

snd :: (a, b) -> b
snd (_, b) = b
\end{code}

\textbf{Между действительными числами:} Кто-то написал очень хорошую
функцию, но она определена на \In{Double}, а вам приходится использовать
\In{Float}. Как быть? Нам поможет функция \In{realToFrac}:


\begin{code}
Prelude> :i realToFrac
realToFrac :: (Real a, Fractional b) => a -> b
  	-- Defined in `GHC.Real'
\end{code}

Она принимает значение из класса \In{Real} и приводит его к значению,
которое можно делить. Что это за класс \In{Real}? Математики наверное
смекнут, что это противоположность комплексным числам (где-то должен
быть определён тип или класс \In{Complex}, и он правда есть, но об этом
в следующем разделе). При переходе к комплексным числам мы теряем
способность сравнения на больше/меньше, но сохраняем возможность
вычисления арифметических операций, поэтому класс \In{Real} это
пересечение классов \In{Num} и \In{Ord}:


\begin{code}
Prelude> :i Real
class (Num a, Ord a) => Real a where
  toRational :: a -> Rational
\end{code}

Здесь ``пересечение'' означает ``и тот и другой''. Пересечение классов
кодируется с помощью контекста. Вернёмся к нашему первому примеру:


\begin{code}
Prelude> realToFrac (1::Float) + (1::Double)
2.0
\end{code}

Отметим, что этой функцией можно пользоваться не только для типов
\In{Float} и \In{Double}, в Haskell возможны самые экзотические числа.

Если преобразования между \In{Float} и \In{Double} происходят
очень-очень часто, возможно имеет смысл воспользоваться специальными для
\In{GHC} функциями: Они определены в модуле \In{GHC.Float}:


\begin{code}
Prelude> :m +GHC.Float
Prelude GHC.Float> :t float2Double
float2Double :: Float -> Double
Prelude GHC.Float> :t double2float
double2Float :: Double -> Float
\end{code}

\section{Документация}

К этой главе мы уже рассмотрели основные конструкции языка и базовые
типы. Если у вас есть какая-то задача, вы уже можете начать её решать.
Для этого сначала нужно будет описать в типах проблему, затем выразить с
помощью функций её решение.

Но не стоит писать все функции самостоятельно, если функция достаточно
общая её наверняка кто-нибудь уже написал. Самые полезные функции и
классы определены в модуле \In{Prelude} и основных стандартных
библиотечных модулях. Было бы излишним описывать каждую функцию, книга
превратилась бы в справочник. Вместо этого давайте научимся искать
функции в документации. Нам понадобится умение составлять типы функций и
небольшое знание английского языка.

Для начала о том, где находится документация к стандартным модулям. Если
вы установили \In{ghc} вместе с \In{Haskell Platform} под Windows скорее
всего во вкладке \In{Пуск}, там где иконка \In{ghc} там же находится и
документация. В Linux необходимо найти директорию с документацией,
скорее всего она в директории \In{/usr/local/share/doc/ghc/libraries}.
Также документацию можно найти в интернете, наберите в поисковике
Haskell Hierarchical Libraries. На главной странице документации вы
найдёте огромное количество модулей. Нас пока интересуют разделы
\In{Data} и \In{Prelude}. Разделы расположены по алфавиту. То что вы
видите это стандартный вид документации в Haskell. Документация делается
с помощью специального приложения \In{Haddock}, мы тоже научимся такие
делать, но позже, пока мы попробуем разобраться с тем как искать в
документации функции.

Предположим нам нужно вычислить длину списка. Нам нужна функция, которая
принимает список и возвращает целое число, скорее всего её тип
\In{[a] -> Int}, обычно во всех библиотечных функциях для целых чисел
используется тип \In{Int}, также на месте параметра используются буквы
\In{a}, \In{b}, \In{c}. Мы можем открыть документацию к \In{Prelude}
набрать в строке поиска тип \In{[a] -> Int}. Или поискать такую функцию
в разделе функций для списков \In{List Operations}. Тогда мы увидим
единственную функцию с таким типом, под говорящим именем \In{length}.
Так мы нашли то, что искали.

Или мы ищем функцию, которая переворачивает список, нам нужна функция с
типом \In{[a] -> [a]}. Таких функций в \In{Prelude} несколько, но имя
\In{reverse} одной из них может намекнуть на её смысл.

Но одной \In{Prelude} мир стандартных функций Haskell не ограничивается,
если вы не нашли необходимую вам функцию в \In{Prelude} её стоит
поискать в других библиотечных модулях. Обычно функции разделяются по
тому на каких типах они определены. Так например функция
\In{sort :: Ord a => [a] -> [a]} определена не в \In{Prelude}, а в
отдельном библиотечном модуле для списков он называется \In{Data.List}.
Так же есть много других модулей для разных типов, таких как
\In{Data.Bool}, \In{Data.Char}, \In{Data.Function}, \In{Data.Maybe} и
многие другие. Не пугайтесь изобилия модулей постепенно они станут вашей
опорой.

Для поиска в стандартных библиотеках есть замечательный интернет-сервис
Hoogle (\url{http://www.haskell.org/hoogle/}). Hoogle может искать
значения не только по имени, но и по типам. Например мы хотим узнать
целочисленный код символа. Поиск по типу \In{Char -> Int} выдаёт искомую
функцию \In{digitToInt}.

\section{Краткое содержание}

В этой главе мы познакомились с интерпретатором \In{ghci} и основными
типами. Рассмотрели много примеров.

\subsubsection{Типы}

\ctable[pos = H, center, botcap]{lll}
{% notes
}
{% rows
\FL
\In{Bool} &  & Основные операции: \In{&&}, \In{||}, \In{not},
\In{if c then t else e}
\\\noalign{\medskip}
\In{Char} &  & Значения пишутся в ординарных кавычках, как в \In{'H'},
\In{'+'}
\\\noalign{\medskip}
\In{String} &  & Значения пишутся в двойных кавычках, как в
\In{}\verb!"!\In{Hello World}\verb!"!\In{}
\\\noalign{\medskip}
\In{Int} &  & Эффективные целые числа, но ограниченные
\\\noalign{\medskip}
\In{Integer} &  & Не ограниченные целые числа, но не эффективные
\\\noalign{\medskip}
\In{Double} &  & Числа с двойной точностью
\\\noalign{\medskip}
\In{Float} &  & Числа с ординарной точностью
\\\noalign{\medskip}
\In{Rational} &  & Дробные числа
\LL
}

Нам впервые встретились кортежи (на функции \In{properFraction}).
Кортежи используются для возвращения из функции нескольких значений.
Элементы кортежа могут иметь разные типы. Для извлечения элементов из
кортежей-пар используются функции \In{fst} и \In{snd}. Кортежи пишутся в
скобках, и элементы разделены запятыми:


\begin{code}
(a, b)
(a, b, c)
(a, b, c, d)
...
\end{code}

\subsubsection{Классы}

\ctable[pos = H, center, botcap]{ll}
{% notes
}
{% rows
\FL
\In{Show} & Печать
\\\noalign{\medskip}
\In{Eq} & Сравнение на равенство
\\\noalign{\medskip}
\In{Num} & Сложение и умножение
\\\noalign{\medskip}
\In{Fractional} & Деление
\LL
}

\subsubsection{Особенности синтаксиса}

Запись применения функции:

\ctable[pos = H, center, botcap]{ll}
{% notes
}
{% rows
\FL
\parbox[b]{0.21\columnwidth}{\raggedright
Префиксная
} & \parbox[b]{0.24\columnwidth}{\raggedright
Инфиксная
}
\ML
\parbox[t]{0.21\columnwidth}{\raggedright
\In{add a b}
} & \parbox[t]{0.24\columnwidth}{\raggedright
\In{a `add` b}
}
\\\noalign{\medskip}
\parbox[t]{0.21\columnwidth}{\raggedright
\In{(+) a b}
} & \parbox[t]{0.24\columnwidth}{\raggedright
\In{a + b}
}
\LL
}

Также мы научились приводить одни численные типы к другим и пользоваться
документацией.

\section{Упражнения}

\begin{itemize}
\item
  Напишите функцию \In{beside :: Nat -> Nat -> Bool}, которая будет
  возвращать \In{True} только в том случае, если два аргумента находятся
  рядом, т.е.\textasciitilde{}один из них можно получить через другой
  операцией \In{Succ}.
\item
  Напишите функцию \In{beside2 :: Nat -> Nat -> Bool}, которая будет
  возвращать \In{True} только если аргументы являются соседями через
  некоторое другое число.
\item
  Мы написали очень неэффективную функцию сложения натуральных чисел.
  Проблема в том, что число рекурсивных вызовов функции зависит от
  величины второго аргумента. Если мы захотим прибавить единицу к сотне,
  то порядок следования аргументов существенно повлияет на скорость
  вычисления. Напишите функцию, которая лишена этого недостатка.
\item
  Напишите функцию возведения в степень \In{pow :: Nat -> Nat -> Nat}.
\item
  Напишите тип, описывающий бинарные деревья \In{BinTree a}. Бинарное
  дерево может быть либо листом со значением типа \In{a}, либо хранить
  два поддерева.
\item
  Напишите функцию \In{reverse :: BinTree a -> BinTree a}, которая
  переворачивает дерево. Она меняет местами два элемента в узле дерева.
\item
  Напишите функцию \In{depth :: BinTree a -> Nat}, которая вычисляет
  глубину дерева, т.е.\textasciitilde{}самый длинный путь от корня
  дерева к листу.
\item
  Напишите функцию \In{leaves :: BinTree a -> [a]}, которая переводит
  бинарное дерево в список, возвращая все элементы в листьях дерева.
\item
  Обратите внимание на раздел \In{List Operations} в \In{Prelude}.
  Посмотрите на функции и их типы. Попробуйте догадаться по типу функции
  и названию что она делает.
\item
  Попробуйте разобраться по документации с классами \In{Ord} (сравнение
  на больше/меньше), \In{Enum} (перечисления) и \In{Integral} (целые
  числа). Также стоит отметить класс \In{Floating}. Если у вас не
  получится, не беда, они обязательно встретятся нам вновь. Там и
  разберёмся.
\item
  Найдите функцию, которая переставляет элементы пары местами (элементы
  могут быть разных типов).\\ Потренируйтесь с кортежами. Определите
  аналоги функций \In{fst} и \In{snd} для не пар. Обратите внимание на
  то, что сочетание символов \In{(,)} это функция-конструктор пары:


  \begin{code}
  Prelude> (,) "Hi" 101
  ("Hi",101)
  Prelude> :t (,)
  (,) :: a -> b -> (a, b)
  \end{code}

  Также определены \In{(,,)}, \In{(,,,)} и другие.
\end{itemize}
